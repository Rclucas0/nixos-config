#+title: NixOS Flake Configuration
#+author: Riley Lucas
#+description: What NixOS is and my configuration


* TABLE OF CONTENTS :toc:
- [[#what-is-nixos][What Is NixOS]]
- [[#useful-nix-commands][Useful Nix Commands]]
- [[#what-are-nix-flakes][What Are Nix Flakes]]
- [[#my-current-nixos-configuration-using-nix-flakes][My Current NixOS Configuration Using Nix Flakes]]
- [[#flakenix-configuration][Flake.nix Configuration]]
- [[#link-to-configurationnix-files][Link To Configuration.nix Files]]
- [[#link-to-dwm-configuration-files][Link To Dwm Configuration Files]]
- [[#user-dotfiles][User Dotfiles]]
  - [[#homenix-configuration][Home.nix Configuration]]
  - [[#starshipnix][Starship.nix]]
  - [[#sxhkd-config][Sxhkd Config]]
  - [[#bash-config][Bash Config]]
  - [[#rofi-config][Rofi Config]]
  - [[#gtk-themeing][Gtk Themeing]]
  - [[#place_holder][Place_Holder]]

* What Is NixOS

- NixOS is a declarative system unlike most systems that you normally see to day which are imperative
- Only difference is that the system configurations other then user customization is all done in one file and is rebuilt on each install
- The configuration is written in a functional programming language called Nix
- The Nix programming uses nix to create and nix package which is then used to

* Useful Nix Commands

- sudo nixos-rebuild switch/test
  * This command will reinstall the entire system and add a new entry to boot loader if used switch, test will do the same other than adding it to boot loader

- sudo nixos-rebuild switch/ test --flake ./path/to/flake.nix
  * Rebuilds the system with a flake rather than default configuration.nixos

- sudo nix-collect-garbage --delete-older-than 15d
  * Will remove unused packages and boot loader entries

* What Are Nix Flakes

From [[https://zero-to-nix.com/concepts/flakes][Zero to Nix]]

- A Nix flake is a directory with a flake.nix and flake.lock at the root that outputs Nix expressions that others can use to do things like build packages, run programs, use development environments, or stand up NixOS systems. If necessary, flakes can use the outputs of other flakes as inputs.

* My Current NixOS Configuration Using Nix Flakes

- This is my current set up of NixOS using a flake to setup the main system
- I am also using emacs org-mode to set up most of this config and what you are reading now

[[./1mon-gruvbox-dwm.png]]

* Flake.nix Configuration

#+begin_src nix :tangle ./flake.nix
{
  description = "My Dwm Flake";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixos-unstable";
    home-manager.url = "github:nix-community/home-manager/master";
    home-manager.inputs.nixpkgs.follows = "nixpkgs";
    nix-colors.url = "github:misterio77/nix-colors";
  };
  outputs = inputs@{ nixpkgs, home-manager, ... }:
  let
    system = "x86_64-linux";

    # User Variables
    hostname = "nixos";
    username = "rileyl";
    gitUsername = "Riley Lucas";
    gitEmail = "rclucas0@gmail.com";
    flakeDir = "/home/${username}/nixos-config";
    theme = "gruvbox-material-dark-hard";

    pkgs = import nixpkgs {
      inherit system;
      config = {
	    allowUnfree = true;
      };
    };
  in {
    nixosConfigurations = {
      "${hostname}" = nixpkgs.lib.nixosSystem {
	    specialArgs = {
          inherit system; inherit inputs;
          inherit username; inherit hostname;
          inherit gitUsername; inherit gitEmail;
          inherit flakeDir;
        };
	    modules = [ ./profiles/personal-dwm/configuration.nix
          home-manager.nixosModules.home-manager {
	        home-manager.extraSpecialArgs = {
              inherit username; inherit gitEmail;
              inherit inputs; inherit gitUsername;
              inherit flakeDir; inherit theme;
              inherit (inputs.nix-colors.lib-contrib {inherit pkgs;}) gtkThemeFromScheme;
            };
	        home-manager.useGlobalPkgs = true;
            home-manager.useUserPackages = true;
            home-manager.backupFileExtension = "backup";
	        home-manager.users.${username} = import ./home.nix;
	      }
	    ];
      };
    };
  };
}
#+end_src

* Link To Configuration.nix [[./system/README.org][Files]]
* Link To Dwm Configuration [[./user/wm/README.org][Files]]
* User Dotfiles

- These are the files that are not controlled by the flake or by the base NixOS config
- Soon will be moved to a home-manager setup

** Home.nix Configuration

#+begin_src nix :tangle ./home.nix
{ gtkThemeFromScheme, config, pkgs, inputs, gitEmail, gitUsername, username, theme, ... }:

{
  # Home Manager needs a bit of information about you and the paths it should
  # manage.
  home.username = "${username}";
  home.homeDirectory = "/home/${username}";
  home.stateVersion = "23.11"; # Please read the comment before changing.

  colorScheme = inputs.nix-colors.colorSchemes."${theme}";

  imports = [
   ./user/starship.nix
    ./user/bash.nix
    ./user/kitty.nix
    ./user/rofi.nix
    ./user/gtk-gt.nix
    inputs.nix-colors.homeManagerModules.default
  ];

  home.file.".config/rofi/rofi.jpg".source = ./user/rofi.jpg;
  home.file.".config/starship.toml".source = ./user/starship.toml;
  home.file.".face".source = ./user/face.jpg;


    # Install & Configure Git
  programs.git = {
    enable = true;
    userName = "${gitUsername}";
    userEmail = "${gitEmail}";
  };

  home.packages = [

  ];

  home.sessionVariables = {
    # EDITOR = "emacs";
  };

  programs.home-manager.enable = true;
}

#+end_src

** Starship.nix

#+begin_src nix :tangle ./user/starship.nix
{ config, pkgs, ... }:

{
  programs.starship = {
    enable = true;
#    settings = {
#      aws.style = "bold #ffb86c";
#      cmd_duration.style = "bold #f1fa8c";
#      directory.style = "bold #50fa7b";
#      hostname.style = "bold #ff5555";
#      git_branch.style = "bold #ff79c6";
#      git_status.style = "bold #ff5555";
#      username = {
#        format = "[$user]($style) on ";
#        style_user = "bold #bd93f9";
#      };
#      character = {
#        success_symbol = "[](bold #f8f8f2)";
#        error_symbol = "[](bold #ff5555)";
#     };
#    };
  };
}

#+end_src

** Sxhkd Config

#+begin_src bash :tangle ~/.config/sxhkd/sxhkdrc
super + BackSpace
	pkill -USR1 -x sxhkd

super + {_}{w,a,Return,e}
	{brave,st,kitty,emacsclient -c -a 'emacs'}

super + shift + {_}{d}
	{rofi -show drun}

alt + {_}{d,s,r,h,m,g}
	{discord,steam,dmenu_run,flatpak run com.heroicgameslauncher.hgl,prismlauncher,gimp}

alt + shift + {_}{p,Return}
	{pavucontrol,thunar}

ctrl + {_}{p}
	{flameshot gui}
#+end_src

** Bash Config

#+begin_src nix :tangle ./user/bash.nix
{ config, pkgs, flakeDir, ... }:

{

  # Configure Bash
  programs.bash = {
    enable = true;
    enableCompletion = true;
    profileExtra = ''
      #if [ -z "$DISPLAY" ] && [ "$XDG_VTNR" = 1 ]; then
      #  exec Hyprland
      #fi
    '';

    initExtra = ''
      neofetch
      export PATH=$PATH:/home/$USER/.config/emacs/bin
      '';

    sessionVariables = {

    };

    shellAliases = {
      sv="sudo vim";
      flake-rebuild="sudo nixos-rebuild switch --flake ${flakeDir}";
      flake-update="sudo nix flake update ${flakeDir}";
      gcCleanup="nix-collect-garbage --delete-old && sudo nix-collect-garbage -d && sudo /run/current-system/bin/switch-to-configuration boot";
      v="vim";
      b="vim";
      nv="nvim";
      ls="lsd";
      ll="lsd -l";
      la="lsd -a";
      lal="lsd -al";
      ".."="cd ..";
    };
  };
}
#+end_src

** Rofi Config

#+begin_src nix :tangle ./user/rofi.nix
{ pkgs, config, ... }:

let
  palette = config.colorScheme.palette;
in {
  home.file.".config/rofi/config.rasi".text = ''
    @theme "/dev/null"

    * {
        bg: #${palette.base00};
        background-color: @bg;
    }

    configuration {
	    show-icons: true;
	    icon-theme: "Papirus";
	    location: 0;
	    font: "Ubuntu 12";
	    display-drun: "Launch:";
    }

    window {
	    width: 35%;
	    transparency: "real";
	    orientation: vertical;
	    border-color: #${palette.base0B};
        border-radius: 10px;
    }

    mainbox {
	    children: [inputbar, listview];
    }


    // ELEMENT
    // -----------------------------------

    element {
	    padding: 4 12;
	    text-color: #${palette.base05};
        border-radius: 5px;
    }

    element selected {
	    text-color: #${palette.base01};
	    background-color: #${palette.base0B};
    }

    element-text {
	    background-color: inherit;
	    text-color: inherit;
    }

    element-icon {
	    size: 16 px;
	    background-color: inherit;
	    padding: 0 6 0 0;
	    alignment: vertical;
    }

    listview {
	    columns: 2;
	    lines: 9;
	    padding: 8 0;
	    fixed-height: true;
	    fixed-columns: true;
	    fixed-lines: true;
	    border: 0 10 6 10;
    }

    // INPUT BAR
    //------------------------------------------------

    entry {
	    text-color: #${palette.base05};
	    padding: 10 10 0 0;
	    margin: 0 -2 0 0;
    }

    inputbar {
	    background-image: url("~/.config/rofi/rofi.jpg", width);
	    padding: 180 0 0;
	    margin: 0 0 0 0;
    }

    prompt {
	    text-color: #${palette.base0D};
	    padding: 10 6 0 10;
	    margin: 0 -2 0 0;
    }
  '';
}
#+end_src

** Gtk Themeing

#+begin_src nix :tangle ./user/gtk-gt.nix
{ pkgs, config, gtkThemeFromScheme, ... }:

{
  # Configure Cursor Theme
  home.pointerCursor = {
    gtk.enable = true;
    x11.enable = true;
    package = pkgs.bibata-cursors;
    name = "Bibata-Modern-Ice";
    size = 24;
  };

  # Theme GTK
  gtk = {
    enable = true;
    font = {
      name = "Ubuntu";
      size = 12;
      package = pkgs.ubuntu_font_family;
    };
    theme = {
      name = "${config.colorScheme.slug}";
      package = gtkThemeFromScheme {scheme = config.colorScheme;};
    };
    iconTheme = {
      name = "candy-icons";
      package = pkgs.candy-icons;
    };
    gtk3.extraConfig = {
      gtk-application-prefer-dark-theme=1;
    };
    gtk4.extraConfig = {
      gtk-application-prefer-dark-theme=1;
    };
  };

  # Theme QT -> GTK
  qt = {
    enable = true;
    platformTheme = "gtk";
    style = {
        name = "adwaita-dark";
        package = pkgs.adwaita-qt;
    };
  };
}
#+end_src

** Place_Holder
